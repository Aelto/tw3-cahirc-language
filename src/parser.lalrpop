use std::cell::RefCell;
use std::rc::Rc;

use crate::ast::{
    ProgramInformation,
    span_manager::{SpanMaker, Spanned as SpannedNode},

    Program,
    Expression,
    ExpressionBody,
    OperationCode,
    TypedIdentifier,
    Statement,
    FunctionDeclaration,
    TypeDeclaration,
    VariableDeclaration,
    FunctionBodyStatement,
    FunctionCallParameters,
    IdentifierTerm,
    AssignmentType,
    IfStatement,
    ComparisonType,
    ForStatement,
    VariableDeclarationOrAssignment,
    VariableAssignment,
    WhileStatement,
    DoWhileStatement,
    ClassDeclaration,
    ClassBodyStatement,
    ClassType,
    EncapsulationType,
    StructDeclaration,
    StructBodyStatement,
    FunctionType,
    FunctionCall,
    EnumDeclaration,
    EnumBodyStatement,
    ClassInstantiation,
    FunctionDeclarationParameter,
    ParameterType,
    BooleanJoinType,
    SwitchStatement,
    SwitchCaseStatement,
    LambdaDeclaration,
    Lambda,
    LambdaType,
    ForInStatement,
    ContextType
};

use crate::ast::codegen::context::Context;

grammar(information: &ProgramInformation, span_maker: &mut SpanMaker<'input>);

pub Program: Program = {
    <statements:(<Statement>)*> => Program { statements }
}

Statement: Statement = {
    GlobalFunctionDeclaration => Statement::FunctionDeclaration(<>),
    ClassDeclaration => Statement::ClassDeclaration(<>),
    StructDeclaration => Statement::StructDeclaration(<>),
    EnumDeclaration => Statement::EnumDeclaration(<>)
};

// -----------------------------------------------------------------------------

ClassDeclaration: ClassDeclaration = {
    <class_type:ClassType> <namel: @L> <name: Identifier> <namer: @R> <generic_types:GenericTypesDeclaration?>
    <parent_class_name:(KeywordIn <Identifier>)?>
    <extended_class_name:(KeywordExtends <Identifier>)?>
    "{" <body_statements:(<ClassBodyStatement>)*> "}"
        => ClassDeclaration {
            context: Rc::new(
                RefCell::new(Context::new(&format!("class: {}", &name),
                generic_types.clone(),
                match &parent_class_name {
                    None => ContextType::ClassOrStruct,
                    Some(parent_class_name) => ContextType::State { parent_class_name: parent_class_name.clone() }
                }
                
            ))),
            class_type,
            name,
            parent_class_name,
            extended_class_name,
            body_statements,
            generic_types,
            span_name: span_maker.span(namel, namer, "class declaration")
        }
}

ClassType: ClassType = {
    KeywordStatemachine KeywordClass => ClassType::StatemachineClass,
    KeywordAbstract KeywordClass => ClassType::Abstract,
    KeywordState => ClassType::State,
    KeywordClass => ClassType::Class,
}

ClassBodyStatement: ClassBodyStatement = {
    <encapsulation:EncapsulationType?> <is_saved:KeywordSaved?> <property_declaration:VariableDeclaration> ";" => ClassBodyStatement::Property {
        encapsulation,
        property_declaration,
        is_saved: is_saved.is_some()
    },

    <encapsulation:EncapsulationType?> <function_declaration:MethodDeclaration> => ClassBodyStatement::Method {
        encapsulation,
        function_declaration
    },

    KeywordDefault <VariableAssignment> ";" => ClassBodyStatement::DefaultValue(<>)
}

EncapsulationType: EncapsulationType = {
    KeywordPublic => EncapsulationType::Public,
    KeywordPrivate => EncapsulationType::Private,
    KeywordProtected => EncapsulationType::Protected
}

// -----------------------------------------------------------------------------

StructDeclaration: StructDeclaration = {
    KeywordStruct <namel: @L> <name: Identifier> <namer: @R> <generic_types:GenericTypesDeclaration?> "{" <body_statements:(<StructBodyStatement>)*> "}"
        => StructDeclaration {
            context: Rc::new(RefCell::new(Context::new(&format!("struct: {}", &name), generic_types.clone(), ContextType::ClassOrStruct))),
            name,
            body_statements,
            span_name: span_maker.span(namel, namer, "struct declaration")
        }
}

StructBodyStatement: StructBodyStatement = {
    <VariableDeclaration> ";" => StructBodyStatement::Property(<>),
    KeywordDefault <VariableAssignment> ";" => StructBodyStatement::DefaultValue(<>)
}

// -----------------------------------------------------------------------------

EnumDeclaration: EnumDeclaration = {
    KeywordEnum <name: Identifier> "{" <body_statements:(<TrailingComma<EnumBodyStatement>>)> "}"
        => EnumDeclaration {
            name,
            body_statements
        }
}

EnumBodyStatement: EnumBodyStatement = {
    <name: Identifier> <number:("=" <Integer>)?> => EnumBodyStatement{<>},
}

// -----------------------------------------------------------------------------

GenericTypesDeclaration: Vec<String> = {
    "<" <SeparatedIdentifier> ">" => <>
}

GlobalFunctionDeclaration: Rc<FunctionDeclaration> = {
    <function_type:GlobalFunctionType> <namel: @L> <name: Identifier> <namer: @R> <generic_types:GenericTypesDeclaration?>
    "(" <parameters:TrailingComma<FunctionDeclarationParameter>> ")" <type_declaration:TypeDeclaration?>
    "{" <body_statements:(<FunctionBodyStatement>)*> "}"
        => {
            let has_generic_types = generic_types.is_some();
            let declaration = Rc::new(FunctionDeclaration {
                context: Rc::new(RefCell::new(Context::new(&format!("function: {}", &name), generic_types.clone(), ContextType::Function))),
                function_type,
                name: name.clone(),
                generic_types,
                parameters,
                type_declaration: type_declaration,
                body_statements,
                span_name: span_maker.span(namel, namer, "function declaration")
            });

            /*if has_generic_types {
                let mut generic_functions_register = information.generic_functions_register.borrow_mut();

                generic_functions_register.register_new_generic_function(&name, declaration.clone());
            }*/
            
            declaration
        }
};

MethodDeclaration: Rc<FunctionDeclaration> = {
    <function_type:FunctionType> <namel: @L> <name: Identifier> <namer: @R> <generic_types:GenericTypesDeclaration?>
    "(" <parameters:TrailingComma<FunctionDeclarationParameter>> ")" <type_declaration:TypeDeclaration?>
    "{" <body_statements:(<FunctionBodyStatement>)*> "}"
        => {
            let has_generic_types = generic_types.is_some();
            let declaration = Rc::new(FunctionDeclaration {
                context: Rc::new(RefCell::new(Context::new(&format!("method: {}", &name), generic_types.clone(), ContextType::Function))),
                function_type,
                name: name.clone(),
                generic_types,
                parameters,
                type_declaration: type_declaration,
                body_statements,
                span_name: span_maker.span(namel, namer, "method declaration")
            });

            /*if has_generic_types {
                let mut generic_functions_register = information.generic_functions_register.borrow_mut();

                generic_functions_register.register_new_generic_function(&name, declaration.clone());
            }*/
            
            declaration
        }
};

FunctionDeclarationParameter: FunctionDeclarationParameter = {
    <l: @L> <param_type:ParameterType?> <typed_identifier:TypedIdentifier> <r: @R> => FunctionDeclarationParameter {
        parameter_type: param_type.unwrap_or(ParameterType::Copy),
        typed_identifier,
        span: span_maker.span(l, r, "function declaration parameter")
    }
}

ParameterType: ParameterType = {
    KeywordOptional => ParameterType::Optional,
    KeywordOut => ParameterType::Reference
}

FunctionType: FunctionType = {
    KeywordTimer KeywordFunction => FunctionType::Timer,
    KeywordEvent => FunctionType::Event,
    KeywordEntry KeywordFunction => FunctionType::Entry,
    GlobalFunctionType => <>
}

GlobalFunctionType: FunctionType = {
    KeywordLatent KeywordFunction => FunctionType::Latent,
    KeywordFunction => FunctionType::Function,
    KeywordExec KeywordFunction => FunctionType::Exec
}

FunctionBodyStatement: FunctionBodyStatement = {
    <var:VariableDeclaration> ";" => FunctionBodyStatement::VariableDeclaration(var),
    <expression:Expression> ";" => FunctionBodyStatement::Expression(expression),
    
    KeywordReturn <expression:Expression?> ";" => FunctionBodyStatement::Return(expression),
    KeywordBreak ";" => FunctionBodyStatement::Break,
    KeywordContinue ";" => FunctionBodyStatement::Continue,
    KeywordDelete <expression:Expression> ";" => FunctionBodyStatement::Delete(expression),

    <var:VariableAssignment> ";" => FunctionBodyStatement::Assignement(<>),
    IfStatement => FunctionBodyStatement::IfStatement(<>),
    ForStatement => FunctionBodyStatement::ForStatement(<>),
    ForInStatement => FunctionBodyStatement::ForInStatement(<>),
    WhileStatement => FunctionBodyStatement::WhileStatement(<>),
    <DoWhileStatement> ";" => FunctionBodyStatement::DoWhileStatement(<>),
    SwitchStatement => FunctionBodyStatement::SwitchStatement(<>)
}

// -----------------------------------------------------------------------------

LambdaDeclaration: LambdaDeclaration = {
    KeywordFn "(" <parameters:TrailingComma<FunctionDeclarationParameter>> ")" <type_declaration:TypeDeclaration?> => {
        LambdaDeclaration {
            parameters,
            type_declaration: type_declaration.and_then(|td| Some(Rc::new(td))),
        }
    }
}

// -----------------------------------------------------------------------------


Lambda: Lambda = {
    <l: @L> "|" <parameters:TrailingComma<FunctionDeclarationParameter>> "|" "{" <body_statements:(<FunctionBodyStatement>)*> "}" <r: @R> => Lambda {
        lambda_type: LambdaType::MultiLine,
        parameters,
        body_statements,
        span: span_maker.span(l, r, "lambda"),
        mangled_accessor: RefCell::new(None),
        captured_variables: RefCell::new(Vec::new())
    },
    <l: @L> "|" <parameters:TrailingComma<FunctionDeclarationParameter>> "|" <body_statement:(<FunctionBodyStatement>)> <r: @R> => Lambda {
        lambda_type: LambdaType::SingleLine,
        parameters,
        body_statements: vec![body_statement],
        span: span_maker.span(l, r, "lambda"),
        mangled_accessor: RefCell::new(None),
        captured_variables: RefCell::new(Vec::new())
    }
}

// -----------------------------------------------------------------------------

VariableAssignment: VariableAssignment = {
    <variable_name:Expression> <assignment_type:AssignmentType> <following_expression:Expression>
        => VariableAssignment {
            variable_name,
            assignment_type,
            following_expression
        }
}

// -----------------------------------------------------------------------------

ClassInstantiation: ClassInstantiation = {
    <l: @L> KeywordNew <class_name:Identifier> <generic_type_assignment:("<" <TrailingComma<Type>> ">")?> KeywordIn <lifetime:Identifier> <r: @R> => ClassInstantiation {
        class_name,
        generic_type_assignment,
        lifetime,
        span: span_maker.span(l, r, "class instantiation")
    }
}

// -----------------------------------------------------------------------------

IfStatement: IfStatement = {
    KeywordIf "(" <condition:Expression> ")" "{" <body_statements:(<FunctionBodyStatement>)*> "}" <else_statements:(<ElseStatement>)*>
        => IfStatement::If {
            condition,
            body_statements,
            else_statements
        }
}

ElseStatement: Box<IfStatement> = {
    KeywordElse <condition:(KeywordIf "(" <Expression> ")")?> "{" <body_statements:(<FunctionBodyStatement>)*> "}"
        => Box::new(IfStatement::Else {
            condition,
            body_statements
        })
}

// -----------------------------------------------------------------------------

ForStatement: ForStatement = {
    KeywordFor "(" <initialization:VariableDeclarationOrAssignment?> ";" <condition:Expression> ";" <iteration:VariableAssignment>  ")"
    "{" <body_statements:(<FunctionBodyStatement>)*> "}"
        => ForStatement { initialization, condition, iteration, body_statements }
}

VariableDeclarationOrAssignment: VariableDeclarationOrAssignment = {
    VariableDeclaration => VariableDeclarationOrAssignment::Declaration(<>),
    VariableAssignment => VariableDeclarationOrAssignment::Assignement(<>)
}

// -----------------------------------------------------------------------------

ForInStatement: ForInStatement = {
    KeywordFor <child:TypedIdentifier> KeywordIn <parent:Expression>
    "{" <body_statements:(<FunctionBodyStatement>)*> "}"
        => ForInStatement {
            child: Rc::new(child),
            parent,
            body_statements,
            indexor_name: RefCell::new(String::new()),
        }
}


// -----------------------------------------------------------------------------

SwitchStatement: SwitchStatement = {
    KeywordSwitch "(" <compared:Expression> ")" "{" <cases:SwitchCaseStatement*> "}" => SwitchStatement {
        compared,
        cases
    }
}

SwitchCaseStatement: SwitchCaseStatement = {
    <cases:Case+> <body_statements:(<FunctionBodyStatement>)*> KeywordBreak ";" => SwitchCaseStatement::Case {
        cases,
        body_statements
    },

    KeywordDefault ":" <body_statements:(<FunctionBodyStatement>)*> KeywordBreak ";" => SwitchCaseStatement::Default {
        body_statements
    }
}

Case: Rc<Expression> = {
    KeywordCase <Expression> ":" => <>
}

// -----------------------------------------------------------------------------

WhileStatement: WhileStatement = {
    KeywordWhile "(" <condition:Expression> ")" "{" <body_statements:(<FunctionBodyStatement>)*> "}"
        => WhileStatement { condition, body_statements }
}

DoWhileStatement: DoWhileStatement = {
    KeywordDo "{" <body_statements:(<FunctionBodyStatement>)*> "}" KeywordWhile "(" <condition:Expression> ")"
        => DoWhileStatement { condition, body_statements }
}

// -----------------------------------------------------------------------------

VariableDeclaration: VariableDeclaration = {
    KeywordVar <typed_identifier: TypedIdentifier> <expression:("=" <Expression>)?> => VariableDeclaration::Explicit {
        declaration: Rc::new(typed_identifier),
        following_expression: expression
    },
    KeywordVar <names:SeparatedIdentifier> <expression:("=" <Expression>)> => VariableDeclaration::Implicit {
        names,
        following_expression: expression
    }
}

// -----------------------------------------------------------------------------

TypedIdentifier: TypedIdentifier = {
    <names:SeparatedIdentifier> <type_declaration:TypeDeclaration> => TypedIdentifier {
        names,
        type_declaration
    }
}

TypeDeclaration: TypeDeclaration = {
    ":" <Type> => <>
}

Type: TypeDeclaration = {
    <type_name:Identifier> <generic_type_assignment:("<" <TrailingComma<Type>> ">")?> => TypeDeclaration::Regular {
        type_name,
        generic_type_assignment,
        mangled_accessor: RefCell::new(None)
    },

    LambdaDeclaration => TypeDeclaration::Lambda(<>)
}

// -----------------------------------------------------------------------------

FunctionCallParameters: FunctionCallParameters = {
    "(" <parameters:SeparatedOptionalExpressions> ")" => FunctionCallParameters(parameters)
}

// -----------------------------------------------------------------------------

pub Expression: Rc<Expression> = {
    CastExpression,
}

CastExpression: Rc<Expression> = {
    <expr:BooleanExpression> KeywordAs <cast:Identifier>  => Rc::new(Expression::new(ExpressionBody::Cast(cast, expr))),
    BooleanExpression,
}

BooleanExpression: Rc<Expression> = {
    BooleanExpression BooleanOperation SubExpression => Rc::new(Expression::new(ExpressionBody::Operation(<>))),
    SubExpression,
}

SubExpression: Rc<Expression> = {
    SubExpression ExprOperation NestedExpression => Rc::new(Expression::new(ExpressionBody::Operation(<>))),
    NestedExpression,
};

NestedExpression: Rc<Expression> = {
    NestedExpression ExprNesting NotExpression => Rc::new(Expression::new(ExpressionBody::Operation(<>))),
    NotExpression,
}

NotExpression: Rc<Expression> = {
    "!" <Factor> => Rc::new(Expression::new(ExpressionBody::Not(<>))),
    Factor,
};

ExprNesting: OperationCode = {
    "." => OperationCode::Nesting,
}

ExprOperation: OperationCode = {
    "+" => OperationCode::Add,
    "-" => OperationCode::Sub,

    ComparisonType => OperationCode::Comparison(<>)
};

BooleanOperation: OperationCode = {
    "&&" => OperationCode::BooleanJoin(BooleanJoinType::And),
    "||" => OperationCode::BooleanJoin(BooleanJoinType::Or),
}

Factor: Rc<Expression> = {
    Factor FactorOp Term => Rc::new(Expression::new(ExpressionBody::Operation(<>))),
    Term,
};

FactorOp: OperationCode = {
    "*" => OperationCode::Mul,
    "/" => OperationCode::Div,
    "%" => OperationCode::Modulo,
    "|" => OperationCode::BitwiseOr,
    "&" => OperationCode::BitwiseAnd,
};

AssignmentType: AssignmentType = {
    "=" => AssignmentType::Equal,
    "+=" => AssignmentType::PlusEqual,
    "-=" => AssignmentType::MinusEqual,
    "*=" => AssignmentType::AsteriskEqual,
    "/=" => AssignmentType::SlashEqual
}

ComparisonType: ComparisonType = {
    "==" => ComparisonType::Equal,
    "!=" => ComparisonType::Different,
    ">" => ComparisonType::Greater,
    ">=" => ComparisonType::GreaterEqual,
    "<" => ComparisonType::Lower,
    "<=" => ComparisonType::LowerEqual
}

Term: Rc<Expression> = {
    Spanned<Integer> => Rc::new(Expression::new(ExpressionBody::Integer(<>))),
    Spanned<Float> => Rc::new(Expression::new(ExpressionBody::Float(<>))),
    Lambda => Rc::new(Expression::new(ExpressionBody::Lambda(<>))),
    "(" <Expression> ")" => Rc::new(Expression::new(ExpressionBody::Group(<>))),
    <l: @L> <s:StringLiteral> <r: @R> => Rc::new(Expression::new(ExpressionBody::String(SpannedNode::new(String::from(s), span_maker.span(l, r, "string literal"))))),
    <l: @L> <c:CharLiteral> <r: @R> => Rc::new(Expression::new(ExpressionBody::Name(SpannedNode::new(String::from(c), span_maker.span(l, r, "char literal"))))),

    IdentifierTerm => Rc::new(Expression::new(ExpressionBody::Identifier(<>))),
    ClassInstantiation => Rc::new(Expression::new(ExpressionBody::ClassInstantiation(<>))),
    <l: @L> <accessor:IdentifierTerm> <generic_types:("::" <GenericTypesDeclaration>)?> <parameters:FunctionCallParameters> <r: @R>
        => {
            let function_call = Rc::new(Expression::new(ExpressionBody::FunctionCall(FunctionCall {
                accessor,
                generic_types,
                parameters,
                span: span_maker.span(l, r, "function call"),
                mangled_accessor: RefCell::new(None),
                infered_function_type: RefCell::new(None)
            })));

            function_call
        },
};

Indexing: Rc<Expression> = {
    "[" <Expression> "]" => <>
}

IdentifierTerm: Box<IdentifierTerm> = {
    <l: @L> <text:Identifier> <r: @R> <indexing:Indexing*> => Box::new(IdentifierTerm {
        text,
        indexing,
        span: span_maker.span(l, r, "identifier term")
    })
}

// -----------------------------------------------------------------------------

Float: String = {
    <int:Integer> <n:r"\.[0-9]*"> => format!("{int}{n}"),
};

Integer: String = {
    "+" <n:r"[0-9]+"> => <>.to_string(),
    "-" <n:r"[0-9]+"> => format!("-{}", <>),
    r"[0-9]+" => <>.to_string(),
};

Boolean: bool = {
    "true" => true,
    "false" => false
}

Identifier: String = {
    IdentifierRegex => String::from(<>)
}

// -----------------------------------------------------------------------------

SeparatedOptionalExpressions = OptionalTrailingComma<Expression>;
SeparatedExpressions = TrailingComma<Expression>;
SeparatedTypedIdentifier = TrailingComma<TypedIdentifier>;
SeparatedIdentifier = TrailingComma<Identifier>;

DotSeparatedIdentifiers = DotSeparated<Identifier>;

OptionalTrailingComma<T>: Vec<Option<T>> = {
    <v:(<ElAndCommas<T>>)*> <e:T?> => match e {
        // for anyone, or future me, reading this:
        // This is the equivalent of: v.flatmap(|x| x.clone()).collect();
        // 
        // basically to convert [[Some(T), None, None], [None, Some(T)]]
        // into [Some(T), None, None, None, Some(T)]
        // All of this because it needs type annotation and i couldn't figure
        // how to access the `T` from the macro in lalrpop syntax, and the doc
        // is lacking a little i must say.
        None => {
            let mut o = Vec::new();

            for x in v {
                for y in x {
                    o.push(y.clone());
                }
            }

            o
        },

        Some(e) => {
            let mut o = Vec::new();

            for x in v {
                for y in x {
                    o.push(y.clone());
                }
            }

            o.push(Some(e));

            o
        }
    },
}

ElAndCommas<T>: Vec<Option<T>> = {
    <v:T> <c:(",")+> => {
        let mut output = Vec::new();

        output.push(Some(v));

        let mut u = (0..c.len() - 1)
            .into_iter()
            .map(|_| None)
            .collect();

        output.append(&mut u);

        output
    }
}

/// A comma separated list of `T` with the possibility of a trailing comma
TrailingComma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

/// A dot separated list of `T` with NO possibility of a trailing dot
DotSeparated<T>: Vec<T> = {
    <e:T> "." <ee:T> <mut v:("." <T> )*> => {
        v.push(e);
        v.push(ee);
        v
    }
};

/// A Tier,
/// 
///
Tier<Op,NextTier>: Rc<Expression> = {
    Tier<Op,NextTier> Op NextTier => Rc::new(Expression::new(ExpressionBody::Operation(<>))),
    NextTier
};

Spanned<T>: SpannedNode<T> = {
    <l: @L> <val: T> <r: @R> => SpannedNode::new(val, span_maker.span(l, r, "spanned"))
};

StringLiteral = {
    r#""[^"]*""#
};

CharLiteral = {
    r#"'[^']*'"#
};

// `match`: Declares the precedence of regular expressions
// relative to one another when synthesizing
// the lexer
match {
    // Ignore C++-style comments
    r"//[^\n\r]*[\n\r]*" => {},
    r"/\*[^\*]*[^/]*(\*/)[\n\r]*" => {},

    // These items have highest precedence.
    r"[0-9]+",

    "function" => KeywordFunction,
    "var" => KeywordVar,
    "return" => KeywordReturn,
    "if" => KeywordIf,
    "else" => KeywordElse,
    "for" => KeywordFor,
    "while" => KeywordWhile,
    "do" => KeywordDo,
    "class" => KeywordClass,
    "statemachine" => KeywordStatemachine,
    "abstract" => KeywordAbstract,
    "state" => KeywordState,
    "extends" => KeywordExtends,
    "private" => KeywordPrivate,
    "public" => KeywordPublic,
    "protected" => KeywordProtected,
    "default" => KeywordDefault,
    "latent" => KeywordLatent,
    "struct" => KeywordStruct,
    "enum" => KeywordEnum,
    "timer" => KeywordTimer,
    "event" => KeywordEvent,
    "in" => KeywordIn,
    "entry" => KeywordEntry,
    "new" => KeywordNew,
    "as" => KeywordAs,
    "optional" => KeywordOptional,
    "out" => KeywordOut,
    "case" => KeywordCase,
    "switch" => KeywordSwitch,
    "break" => KeywordBreak,
    "continue" => KeywordContinue,
    "exec" => KeywordExec,
    "delete" => KeywordDelete,
    "fn" => KeywordFn,
    "saved" => KeywordSaved,
} else {
    // These items have next highest precedence.

    // Given an input like `123`, the number regex above
    // will match; but otherwise, given something like
    // `123foo` or `foo123`, this will match.
    //
    // Here, we also renamed the regex to the name `ID`, which we can
    // use in the grammar itself.
    r"\w+" => IdentifierRegex,

    // Ignore whitespace
    // You probably want this to have low precedence
    r"\s*" => {},

    // This `_` means "add in all the other strings and
    // regular expressions in the grammer here" (e.g.,
    // `"("`).
    _
} // you can have more `else` sections if you like